# Copyright (c) 2022 Macro.
# Use of this source code is governed by the MIT license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//electron/buildflags/buildflags.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//v8/gni/v8.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_OFFICE=$enable_office" ]
}

assert(enable_office)

lok_root_dir = "//third_party/libreofficekit/instdir"
if (is_mac) {
  if (target_cpu == "x64") {
    lok_root_dir += "/x64" 
  } else if (target_cpu == "arm64" ) {
    lok_root_dir += "/aarch64" 
  }
}
lok_sdk_dir = "$lok_root_dir/sdk/v8"

copy("copy_libreofficekit") {
  sources = [
    "$lok_root_dir/LICENSE",
    "$lok_root_dir/program",
    "$lok_root_dir/presets",
    "$lok_root_dir/share",
  ]

  outputs = [ "$root_out_dir/libreofficekit/{{source_file_part}}" ]
}

config("electron_config") {
  include_dirs = ["//electron"]
}

source_set("internal") {
  visibility = [ ":*" ]

  sources = [
    "document_client.cc",
    "document_client.h",
    "event_bus.cc",
    "event_bus.h",
    "lok_tilebuffer.cc",
    "lok_tilebuffer.h",
    "lok_callback.cc",
    "lok_callback.h",
    "office_singleton.cc",
    "office_singleton.h",
    "async_resolver_scope.cc",
    "async_resolver_scope.h",
    "office_client.cc",
    "office_client.h",
    lok_sdk_dir + "/unov8.hxx",
    lok_sdk_dir + "/unov8.cxx",
    "office_keys.cc",
    "office_keys.h",
  ]

  # configs -= [
  #   "//build/config/compiler:no_exceptions",
  #   "//build/config/compiler:no_rtti",
  # ]
  configs += [ lok_sdk_dir + ":libreoffice_lib_config" ]

  deps = [
    ":buildflags",
    "//base",
    "//components/plugins/renderer",
    "//components/strings",
    "//gin",
    "//third_party/blink/public:blink_headers",
    "//third_party/icu",
    "//ui/base",
    "//ui/base/cursor/mojom:cursor_type",
    "//ui/events:events_base",
    "//ui/events/blink",
    "//ui/gfx/codec",
    "//ui/gfx/range",
  ]

  configs += [ ":electron_config" ]

  public_deps = [
    "//skia",
    "//v8",
  ]
}

static_library("office_web_plugin") {
  assert(enable_plugins,
         "The LibreOffice viewer relies on plugin infrastructure")

  sources = [ "office_web_plugin.cc", "office_web_plugin.h" ]

  configs += [ ":electron_config" ]
  configs += [ lok_sdk_dir + ":libreoffice_lib_config" ]

  lib_dirs = [ "$root_out_dir/libreofficekit/program" ]
  libs = []
  ldflags = []

  if (is_win) {
    libs += []
  }

  if (is_linux) {
    libs += []
    ldflags += [ "-Wl,-rpath,\$ORIGIN/libreofficekit/program" ]
  }

  public_deps = [
    ":copy_libreofficekit"
  ]

  deps = [
    ":buildflags",
    ":internal",
    "//base",
    "//base:i18n",
    "//cc/paint",
    "//gin",
    "//skia",
    "//third_party/blink/public:blink_headers",
    "//ui/base/cursor",
    "//ui/base/cursor/mojom:cursor_type",
    "//ui/events/blink",
    "//v8",
  ]
}
